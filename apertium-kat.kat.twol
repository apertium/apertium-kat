Alphabet
 A B Ç D E Ä F G H I J Ž K L M N Ň O Ö P R S Ş T U Ü W Y Ý Z
 a b ç d e ä f g h i j ž k l m n ň o ö p r s ş t u ü w y ý z
 ა ბ გ დ ე ვ ზ თ ი კ ლ მ ნ ო პ ჟ რ ს ტ უ ფ ქ ღ ყ შ ჩ ც ძ წ ჭ ხ ჯ ჰ
 ჱ ჲ ჳ ჴ ჵ  ! old letters
 %{ი%}:ი %{ი%}:0 ! ი that deletes after all vowels
 %{i%}:ი %{i%}:0 ! ი that deletes after only non-truncating vowels
 %{I%}:ი %{I%}:0 ! ი at end of instrumental after non-truncating vowels
 %{A%}:ა %{A%}:0 ! ა at end of ergative after C-final stems
 %{ა%}:ა %{ა%}:0 ! ა that deletes after vowels / truncating ა
 %{ო%}:ო %{ო%}:0 ! ო that deletes after vowels
 %{O%}:ო %{O%}:ვ ! ო that is ვ after vowels
 %{ე%}:ე %{ე%}:0 ! ე that truncates if following consonant can syllabify as new syllable
 %{e%}:ე %{e%}:0 ! ე that deletes after vowels
 %{მ%}:ე %{მ%}:0 ! მ that deletes after vowels
 %{w%}:უ %{w%}:0 ! usually realised as 0, but before version vowel {ი} realised as უ and causes version {ი} to delete (e.g., interaction between 3rd person morphology and version vowel in Perfective series verbs)
 %>:0  ;

Sets

Vow = ა ე ი ო უ ;

TruncV = %{ა%} ;

VNoDelAfterTrunc = %{i%} ;

Rules

"{ი} as 0 after vowels and {w}"
%{ი%}:0 <=> [ Vow: | TruncV: | %{w%}: ] %>: _ ;

"{i} as 0 after non-truncating vowels"
%{i%}:0 <=> [ :Vow - TruncV: ] %>: _ ;

"{A} as 0 after vowel-final stem"
%{A%}:0 <=> :Vow %>: [ მ | %{მ%}: ] _ ;

"{I} as 0 if previous {i} surfaces"
%{I%}:0 <=> %{i%}:ი თ _ ;

"{ა} as 0 after any vowel and (in truncating use) before any vowel"
%{ა%}:0 <=> [ Vow: | TruncV: ] %>: _ ;
            _ %>: [ Vow: | VNoDelAfterTrunc: ] ;

"{ო} as 0 after any vowel"
%{ო%}:0 <=> [ Vow: | TruncV: ] %>: _;

"{O} as ვ after any vowel"
%{O%}:ვ <=> [ Vow: | TruncV: ] %>: _;

"{ე} as 0 before vowel-initial suffix (except nominative and vocative)"
%{ე%}:0 <=> _ : %>: [ :Vow - [ %{ი%}: | %{O%}: | %{ო%}: ] ] ;

"{მ} as 0 after vowel-final stems"
%{მ%}:0 <=> Vow: %>: _ ;

"{e} as 0 after vowels"
%{e%}:0 <=> [ Vow: | TruncV: ] %>: _ ;

"{w} as უ before {ი}:0"
%{w%}:უ <=> _ %>: %{ი%}: ;



! Resources:
! https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! https://wiki.apertium.org/wiki/Twol
! https://wiki.apertium.org/wiki/Hfst

